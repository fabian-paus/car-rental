package de.whs.bpm.car_rental_dsl

import java.util.Calendar;
import de.whs.bpm.car_rental_dsl.RentalDay;
import de.whs.bpm.car_rental_dsl.RentalRequest;
import de.whs.bpm.car_rental_dsl.Customer;

function boolean isWeekend(Calendar day) {
	return day.get(Calendar.DAY_OF_WEEK) == Calendar.SATURDAY
			|| day.get(Calendar.DAY_OF_WEEK) == Calendar.SUNDAY;
}

function boolean isSpecialDay(Calendar day) {
	return day.get(Calendar.DAY_OF_MONTH) == 1 && day.get(Calendar.MONTH) == 0
		|| day.get(Calendar.DAY_OF_MONTH) == 24 && day.get(Calendar.MONTH) == 11
		|| day.get(Calendar.DAY_OF_MONTH) == 31 && day.get(Calendar.MONTH) == 11
	;
}

declare IsNovice
	customer: Customer
end


rule "Daily base price: Small class car"

    when
        $d : RentalDay( carClass == "Small" )
    then
        $d.setPrice(4000);

end

rule "Daily base price: Compact class car"

    when
        $d : RentalDay( carClass == "Compact" )
    then
        $d.setPrice(5000);

end

rule "Daily base price: Middle class car"

    when
        $d : RentalDay( carClass == "Middle" )
    then
        $d.setPrice(7000);

end

rule "Daily base price: Upper class car"

    when
        $d : RentalDay( carClass == "Upper" )
    then
        $d.setPrice(9000);

end

rule "Daily price: Weekend discount"

    when
        $d : RentalDay( isWeekend(day) || isSpecialDay(day) )
    then
        int price = $d.getPrice();
        int discount = (int)(price * 0.25f);
        $d.setPrice(price - discount);

end

rule "Daily price: free seventh"

    when
        $d : RentalDay( dayIndex == 7 || dayIndex == 14 || dayIndex == 21 || dayIndex == 28 )
    then
        $d.setPrice(0);

end

rule "Sum daily prices"

	when
		$r : RentalRequest( )
		$d : RentalDay( request == $r )
	then
		$r.addToBasePrice($d.getPrice());

end

rule "Is driving novice"
	
	when
		$c : Customer( age < 21 || drivingLicense < 2 )
	then
		insert( new IsNovice($c) );
end

rule "Novice: extra charge"

	when
		$r : RentalRequest( )
		$c : Customer( )
		IsNovice(customer == $c)
	then
		$r.setNovice(true);
		$r.setExtraChargePercent(10);
end

rule "Novice: permission required"

	when
		$r : RentalRequest( carClass != "Small" || carClass != "Compact" )
		$c : Customer( )
		IsNovice(customer == $c)
	then
		$r.setRequiresNovicePermission(true);
end

rule "Novice: check passed 1"

	when
		$r : RentalRequest( )
		$c : Customer( )
		not IsNovice(customer == $c)
	then
		$r.setNoviceCheckPassed(true);
end

rule "Novice: check passed 2"

	when
		$r : RentalRequest( carClass == "Small" || carClass == "Compact" )
		$c : Customer( )
		IsNovice(customer == $c)
	then
		$r.setNoviceCheckPassed(true);
end
