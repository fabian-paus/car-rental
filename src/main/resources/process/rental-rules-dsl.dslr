package de.whs.bpm.car_rental_dsl2

import java.util.Calendar;
import de.whs.bpm.car_rental_dsl.RentalDay;
import de.whs.bpm.car_rental_dsl.RentalRequest;
import de.whs.bpm.car_rental_dsl.Garage;
import de.whs.bpm.car_rental_dsl.Customer;

expander rental-language.dsl

function boolean isWeekend(Calendar day) {
	return day.get(Calendar.DAY_OF_WEEK) == Calendar.SATURDAY
			|| day.get(Calendar.DAY_OF_WEEK) == Calendar.SUNDAY;
}

function boolean isHoliday(Calendar day) {
	int dayOfMonth = day.get(Calendar.DAY_OF_MONTH);
	int month = day.get(Calendar.MONTH);
	// Add holidays here (month is zero-based):
	return dayOfMonth ==  1 && month ==  0
		|| dayOfMonth == 25 && month ==  2
		|| dayOfMonth == 28 && month ==  2
		|| dayOfMonth == 24 && month == 11
		|| dayOfMonth == 31 && month == 11
	;
}

function boolean isAvailable(Garage garage, String carClass) {
	return garage.getCount(carClass) > 0;
}

// Marks a customer as a driving novice
declare Novice
	customer: Customer
end

// Marks a car class as available upgrade
declare Upgrade
	carClass : String
end

// Novice check

rule "Is driving novice" ruleflow-group "novice-check"
	when
		there is a request
		there is a customer with that request
		- age is less than 21 or has driving license for less than 2	
	then
		the customer is a driving novice	
end

rule "Extra charge if at least one customer is a novice" ruleflow-group "novice-check"
	when
		there is a request
		there exists a customer who is driving novice
	then
		set extra charge to 10%	
end

rule "Permission for higher classes required" ruleflow-group "novice-check"
	when
		there is a request
		- car class is not in ("Small", "Compact")
		there exists a customer who is driving novice
	then
		ask the boss for permission
end


// Availability check

rule "Downgrade for upper class if not available" ruleflow-group "availibility-check"
	when
		there is a request
		- car class is "Upper"
		- car is not available
	then
		set car class to "Middle"
		set extra deduction to 10%
		update the request
end

rule "Upgrade from Small to Compact" ruleflow-group "availibility-check"
	when
		there is a request
		- car class is "Small"
		- "Compact" car is available
		there exists no upgrade
	then
		upgrade to "Compact" is possible	
end

rule "Upgrade from Small to Middle" ruleflow-group "availibility-check"
	when
		there is a request
		- car class is "Small"
		- "Middle" car is available
		there exists no upgrade
	then
		upgrade to "Middle" is possible	
end

rule "Upgrade from Small to Upper" ruleflow-group "availibility-check"
	when
		there is a request
		- car class is "Small"
		- "Upper" car is available
		there exists no upgrade
	then
		upgrade to "Upper" is possible	
end

rule "Upgrade from Compact to Middle" ruleflow-group "availibility-check"
	when
		there is a request
		- car class is "Compact"
		- "Middle" car is available
		there exists no upgrade
	then
		upgrade to "Middle" is possible	
end

rule "Upgrade from Compact to Upper" ruleflow-group "availibility-check"
	when
		there is a request
		- car class is "Compact"
		- "Upper" car is available
		there exists no upgrade
	then
		upgrade to "Upper" is possible	
end

rule "Upgrade from Middle to Upper" ruleflow-group "availibility-check"
	when
		there is a request
		- car class is "Middle"
		- "Upper" car is available
		there exists no upgrade
	then
		upgrade to "Upper" is possible	
end

rule "If updgrade possible it requires permission" ruleflow-group "availibility-check"
	when
		there is a request
		- car is not available
		there is a possible upgrade
	then
		upgrade the car class
		mark the car for that request as available
end

rule "A car is available" ruleflow-group "availibility-check"
	when
		there is a request
		- car is available
	then
		mark the car for that request as available
end

// Base price

rule "Daily price: Small class car" ruleflow-group "price-calculation"
    when
    	there is a request
    	- car class is "Small"
    	there is a rental day from that request
    then
    	set the daily price to 40.00
end

rule "Daily price: Compact class car" ruleflow-group "price-calculation"
    when
    	there is a request
    	- car class is "Compact"
    	there is a rental day from that request
    then
    	set the daily price to 50.00
end

rule "Daily price: Middle class car" ruleflow-group "price-calculation"
    when
    	there is a request
    	- car class is "Middle"
    	there is a rental day from that request
    then
    	set the daily price to 70.00
end

rule "Daily price: Upper class car" ruleflow-group "price-calculation"
    when
    	there is a request
    	- car class is "Upper"
    	there is a rental day from that request
    then
    	set the daily price to 90.00
end

rule "Daily price: Weekend discount" ruleflow-group "price-calculation"
    when
    	there is a request
    	there is a rental day from that request
    	- is weekend or holiday
    then
    	the daily price is discounted by 25%
end

rule "Daily price: Free seventh day" ruleflow-group "price-calculation"
    when
    	there is a request
    	there is a rental day from that request
    	- day index is in (7, 14, 21, 28)
    then
    	set the daily price to 0.00
end

rule "Base price is the sum of daily prices" ruleflow-group "price-calculation"
	when
		there is a request
		there is a rental day from that request
	then
		add the daily price to the base price
end

// Discount

rule "Discount: All customers are new customers" ruleflow-group "price-calculation"
	when
		there is a request
		every customer from that request is a new customer
	then
		add 10.00 to the discount
end

rule "Discount: All customers have an old reclamation" ruleflow-group "price-calculation"
	when
		there is a request
		every customer from that request had a valid reclamation
	then
		discount the base price by 20%
end

rule "Discount: All customers have participated in security training" ruleflow-group "price-calculation"
	when
		there is a request
		every customer from that request had a security training
	then
		discount the base price by 5%;
end

rule "Discount is limited to 100 €" ruleflow-group "price-calculation"
	when
		there is a request
		- discount is greater than 100.00
	then
		set the discount to 100.00
end

// Final price

rule "Final Price: Calculation" ruleflow-group "price-calculation"
	when
		there is a request
	then
		set the final price to the base price
		subtract the discount from the final price
end

rule "Final Price: Automatic fee" ruleflow-group "price-calculation"
	when
		there is a request
		- for an automatic car
	then
		add a 5% fee of the base price to the final price
end

// Total price

rule "Total Price" ruleflow-group "price-calculation"
	when
		there is a request
	then
		set percent to 100% plus extra charge minus extra deduction
		set the total price to percent% of the final price
end
